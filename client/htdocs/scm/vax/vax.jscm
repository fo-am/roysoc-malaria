;; -*- mode: scheme; -*-
;; Royal Soc Malaria Copyright (C) 2018 FoAM Kernow

(load "flx/scm/canvas.jscm")
(load "flx/scm/canvas-widgets.jscm")

(define receptor-create-distance 0.5)
(define receptor-types (list "A" "B" "C" "D"))

(define (hex->col r g b) (vector (/ r 255) (/ g 255) (/ b 255)))

;d2e049 ff yellow
;444545 ff grey
;7be0ad ff green
;0081af ff blue
;f61067 ff pink

(define receptor-a-col (hex->col 0xd2 0xe0 0x49))
(define receptor-b-col (hex->col 0x44 0x45 0x45))
(define receptor-c-col (hex->col 0x7b 0xe0 0xad))
(define receptor-d-col (hex->col 0x00 0x81 0xaf))

(define (type->col type)
  (cond 
   ((eq? type "A") receptor-a-col)
   ((eq? type "B") receptor-b-col)
   ((eq? type "C") receptor-c-col)
   (else receptor-d-col)))

(define parasite-create-distance 10)
(define parasite-num-receptors 8)

;; 85 6d 9b b6

(define parasite-col (vector (/ 0x85 255)
			     (/ 0x6d 255)
			     (/ 0x9b 255)))
(define parasite-repel 0.03)
(define parasite-cluster -0.002)
(define parasite-inertia 0.90)
(define parasite-max-age 100)
(define parasite-growth-speed 0.3)
(define parasite-max-pop 60)
(define parasite-spawn-distance 1)

(define parasites-start-num 10)

(define vax-swarm-size 300)
(define vax-inject-amount 100)
(define vax-inertia 0.02) ;; 0.05
(define vax-speed -0.5)
(define vax-collide-distance 2)
(define vax-rot-speed 13)

(define explosion-max-age 2)
(define explosion-speed 0.05)

;; posisiton is relative to parasite
(define (make-receptor type pos parasite-id) 
  (list type pos parasite-id))

;; don't need root, just parent and forget...
(define (receptor-type r) (list-ref r 0))
(define (receptor-pos r) (list-ref r 1))
(define (receptor-parasite-id r) (list-ref r 2))

(define (build-receptor pos type pid)
  ;; parenting and forgetting - will be deleted when
  ;; parent is destroyed
  (with-state
   (colour (type->col type))
   (parent pid)
   (translate pos)
   (hint-nozwrite)
   (texture (load-texture "recep-circle.png"))
   ;; (cond 
   ;;  ((eq? type "A") (texture (load-texture "recep-circle.png")))
   ;;  ((eq? type "B") (texture (load-texture "recep-circle.png")))
   ;;  ((eq? type "C") (texture (load-texture "recep-circle.png")))
   ;;  (else (texture (load-texture "recep-circle.png"))))
   (shader-assign-compiled "receptor")   
   (load-primitive "models/planesmall.obj"))
  (make-receptor type pos pid))

(define (receptors-clone rs pid)
  (map
   (lambda (r)
     (build-receptor (receptor-pos r) (receptor-type r) pid))
   rs))

(define squash 0.4)
  
(define (build-receptors n magic pid)
  (let ((start (* (rndf) 2 3.141))
	(primary (choose receptor-types)))    
    (cons
     (build-receptor 
      (vmul (vector (sin start) (* (cos start) squash) 0) receptor-create-distance)
      magic pid)
     (build-list
      (- n 1)
      (lambda (i) 
	(build-receptor 
	 (vmul (vector (sin (+ start (* (/ (+ i 1) n) 2 3.141))) 
		       (* (cos (+ start (* (/ (+ i 1) n) 2 3.141))) squash)
		       0)
	       receptor-create-distance)
	 (if (> (rndf) 0.75) 
	     (choose receptor-types) 
	     primary) pid))))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (make-parasite id pos vel age receptors root)
	(list id pos vel age receptors root))

(define (parasite-id p) (list-ref p 0))
(define (parasite-pos p) (list-ref p 1))
(define (parasite-vel p) (list-ref p 2))
(define (parasite-age p) (list-ref p 3))
(define (parasite-receptors p) (list-ref p 4))
(define (parasite-root p) (list-ref p 5))

(define (parasite-prim)
  (with-state
   (hint-nozwrite)
   (shader-assign-compiled "parasite")
   (colour parasite-col)
   (texture (load-texture "noise.png"))
   (translate (vector 1000 0 0))
   (load-primitive "models/malaria.obj")))

;; lazily reuse primitive ids as parasite ids
(define (build-parasite pos magic)
  (let ((pid (parasite-prim)))
    (make-parasite
     pid pos 
     (vector 0 0 0) 
     (* (rndf) 100)
     (build-receptors parasite-num-receptors magic pid)
     pid)))

(define (parasite-spawn pos parent-receptors)
  (let ((pid (parasite-prim)))
    (make-parasite
     pid pos 
     (vector 0 0 0) 
     (random 10)
     ;; todo: inherit
     (receptors-clone parent-receptors pid)
     pid)))

(define (parasite-closest parasite p)
  (let ((closest 999999))
    (foldl
     (lambda (other r)
       (if (not (eq? (parasite-id other) 
		     (parasite-id parasite)))
	   (let ((dist (vdist (parasite-pos parasite)
			      (parasite-pos other))))
	     (if (< dist closest)
		 (begin
		   (set! closest dist)
		   (parasite-pos other))
		 r))
	   r))
     (vector 0 0 0) p)))

(define (parasite-swarm vel pos closest)
  (let ((diff (vsub pos closest)))
    (vadd
     (vmul vel parasite-inertia)
     (vmul
      (vadd 
       (vmul (vmul (vnormalise diff) parasite-repel) 
	     (Math.pow (/ 1 (vmag diff)) 1.5))
       (vmul (vnormalise pos) parasite-cluster))
      (/ 1 parasite-inertia)))))

(define (update-parasite p parasites)
  (let ((new-vel (parasite-swarm 
		  (parasite-vel p)
		  (parasite-pos p)
		  (parasite-closest p parasites))))
    (with-primitive
     (parasite-root p)
     (identity)
     (translate (parasite-pos p))
     (let ((sc (+ 2 (* (/ (parasite-age p) parasite-max-age) 4))))
       (scale (vector sc sc 0.01))))
    (make-parasite
     (parasite-id p)
     (vadd (parasite-pos p) new-vel)
     new-vel
     (+ (parasite-age p) parasite-growth-speed) 
     (parasite-receptors p)
     (parasite-root p))))

(define (parasites-spawn parasites)
  (foldl
   (lambda (p r)
     (cond 
      ((> (parasite-age p) parasite-max-age)
       (destroy (parasite-root p))
       (if (< (length parasites) parasite-max-pop)
	   (let ((new-pos (vector 1 0 0)))
	     (append
	      (list (parasite-spawn 
		     (vadd (parasite-pos p) 
			   (vmul new-pos parasite-spawn-distance))
		     (parasite-receptors p))
		    (parasite-spawn 
		     (vadd (parasite-pos p) 
			   (vmul new-pos (- parasite-spawn-distance)))
		     (parasite-receptors p)))
	      r))
	   r))
      (else (cons p r))))
   '()
   parasites))

(define (parasites-destroy parasites destroyed)
  (foldl
   (lambda (p r)
     (cond 
      ((list-contains? destroyed (parasite-id p))
       (destroy (parasite-root p))
       r)
      (else (cons p r))))
   '()
   parasites))

(define (parasites-destroy-all parasites)
  (for-each
   (lambda (p)
     (destroy (parasite-root p)))
   parasites))

(define (trnc num)
  (/ (floor (* num 100)) 100))

    ;; (if (> vaccinations 1)
    ;; 	(fire-popup! "bad" (list (+ "CURED IN " (trnc (- (time) game-start-time)) " SECS & " vaccinations " VACCINATIONS USED")
    ;; 				 "YOU CAN DO BETTER"))
    ;; 	(fire-popup! "bad" (list (+ "CURED IN " (trnc (- (time) game-start-time)) " SECS & SPECIAL VAXINATION DISCOVERED")
    ;; 				 "WELL DONE!")))


(define (update-parasites parasites)
  (cond 
   ((zero? (length parasites)) ;; win
    (cond 
     ((> vaccinations 1)
      (fire-popup! "bad" (list (+ "CURED: " vaccinations " VACCINATIONS USED")
			       "YOU CAN DO BETTER")))
     (else
      (set! vaccination-run (+ vaccination-run 1))
      (cond 
       ((> vaccination-run 2)
	(set! vaccination-run 0)
	(fire-popup! "bad" (list "CURED IN ONE: THREE IN A ROW"
				 "YOU ARE A PRO!")))
       (else
	(fire-popup! "bad" (list "CURED: SPECIAL VACCINATION DISCOVERED"
				 "WELL DONE! CAN YOU FIND THREE IN A ROW?"))))))
    (set! new-game-time (+ (time) popup-lifespan))
    (set! reset-game #t)
    parasites)
   ((> (length parasites) parasite-max-pop) ;; lose
    (set! new-game-time (+ (time) popup-lifespan))
    (set! reset-game #t)
    (fire-popup! "bad" (list "INFECTION OUT OF CONTROL" "TRY AGAIN"))
    parasites)
   (else
    (parasites-spawn
     (map
      (lambda (p)
	(update-parasite p parasites))
      parasites)))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (parasites-search pid parasites)
  (cond
   ((null? parasites) #f)
   ((eq? (parasite-id (car parasites)) pid)
    (car parasites))
   (else
    (parasites-search pid (cdr parasites)))))
   
(define (build-parasites n magic)
  (build-list
   n
   (lambda (_)
     (build-parasite 
      (vmul (vector (crndf) (crndf) 0)
	    parasite-create-distance)
      magic))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; vax lifetime

(define vax-state-idle 0)
(define vax-state-syringe 1)
(define vax-state-chasing 2)

(define (make-vax state pid dest vel pos root collide type rot)
  (list state pid dest vel pos root collide type rot))

(define (vax-state v) (list-ref v 0))
(define (vax-pid v) (list-ref v 1))
(define (vax-dest v) (list-ref v 2))
(define (vax-vel v) (list-ref v 3))
(define (vax-pos v) (list-ref v 4))
(define (vax-root v) (list-ref v 5))
(define (vax-collide v) (list-ref v 6))
(define (vax-type v) (list-ref v 7))
(define (vax-rot v) (list-ref v 8))

(define (build-vax)
  (make-vax
   vax-state-idle -1 (vector 0 0 0) (vector 0 0 0) (vector 0 0 0)
   (with-state
    (hint-nozwrite)
    (texture (load-texture "vax.png"))
    (shader-assign-compiled "vax")
    (load-primitive "models/plane.obj"))
   #f "A" 0))

(define (calc-vax-swarm pos vel dest)
  (let ((v (vmul (vnormalise (vsub pos dest)) vax-speed)))
    (vadd (vmul v vax-inertia)
	  (vmul vel (- 1 vax-inertia))
	  )))

(define (vax-inject vs)
  (map
   (lambda (v)
     (cond 
      ((eq? (vax-state v) vax-state-syringe)
       (make-vax
	vax-state-chasing
	-1
	(vector 0 0 0)
	(vector 0 (+ 0.1 (* (rndf) 0.2)) 0)
	(vadd (vector (+ (crndf) 0.3) (* (crndf) 2.5) 0) (vector 0 -13.3 0))
	(vax-root v)
	#f
	(vax-type v)
	(random 360)))
      (else v)))
   vs))

(define (vax-idle vs)
  (map
   (lambda (v)
     (make-vax
      vax-state-idle
      -1
      (vector 0 0 0)
      (vector 0 0 0)
      (vector 0 0 0)
      (vax-root v)
      #f
      (vax-type v)
      (random 360)))
   vs))

(define (vax-in-syringe vs type)
  (define count vax-inject-amount)
  (map
   (lambda (v)
     (cond 
      ((and (> count 0) (eq? (vax-state v) vax-state-idle))
       (set! count (- count 1))
       (with-primitive (vax-root v) (colour (type->col type)))
       (make-vax
	vax-state-syringe
	-1
	(vector 0 0 0)
	(vector 0 0 0)
	(vadd (vector (crndf) (* (crndf) 2.5) 0) (vector -0.05 -13.3 0))
	(vax-root v)
	#f type (random 360)))
      (else v)))
   vs))

(define (vax-choose-receptor type receptors)
  (foldl
   (lambda (rec r)
     (if (and (not r) (eq? (receptor-type rec) type))
	 rec r))
   #f receptors))

;; returns (pid,receptor)
(define (vax-choose-parasite/receptor type parasites)
  (foldl
   (lambda (p r)
     (if (not r)
	 (let ((receptor (vax-choose-receptor type (parasite-receptors p))))
	   (if receptor (list p receptor) #f))
	 r))
   #f
   (shuffle parasites)))

(define (update-vax-swarm vs parasites)
  (map
   (lambda (v)
     (with-primitive
      (vax-root v) 
      (identity)
      (if (eq? (vax-state v) vax-state-idle)
	  (hide #t) (hide #f))
      (if (eq? (vax-pid v) vax-state-syringe) 
	  (translate (vadd (vector (crndf) (crndf) 0) (vax-pos v)))
	  (translate (vax-pos v)))
      (rotate (vector 0 0 (vax-rot v))))
     
     (if (not (eq? (vax-state v) vax-state-chasing))
	 (make-vax
	  (vax-state v)
	  (vax-pid v)
	  (vax-dest v)
	  (vax-vel v) 
	  (vax-pos v) 
	  (vax-root v)
	  #f
	  (vax-type v)
	  (+ (vax-rot v) vax-rot-speed))
	 (let ((p (parasites-search (vax-pid v) parasites)))
	   (cond 
	    (p ;; still alive - keep chasing
	     (let ((dist (vdist (vax-pos v) (vax-dest v))))
	       (when (< dist vax-collide-distance)	
		     (set! explosions (explosions-add explosions (vax-dest v) (vax-type v))))
	       (make-vax
		(if (< dist vax-collide-distance) vax-state-idle vax-state-chasing)
		(vax-pid v)
		(vadd (vax-dest v) (parasite-vel p)) 
		(calc-vax-swarm (vax-pos v) (vax-vel v) (vax-dest v))
		(vadd (vax-pos v) (vax-vel v))
		(vax-root v)
		(< dist vax-collide-distance)
		(vax-type v)
		(+ (vax-rot v) vax-rot-speed))))
	    (else ;; it's died (or first time), find a new one...
	     (let ((p (vax-choose-parasite/receptor (vax-type v) parasites)))
	       (if (not p) ;; no new ones exist
		   (make-vax
		    (if (< (rndf) 0.01) vax-state-idle vax-state-chasing) 
		    -1 
		    (vax-dest v)  
		    (calc-vax-swarm (vax-pos v) (vax-vel v) (vax-dest v))
		    (vadd (vax-pos v) (vax-vel v))
		    (vax-root v)
		    #f (vax-type v) (+ (vax-rot v) 1))
		   (let ((parasite (car p)) 
			 (receptor (cadr p)))
		     (with-primitive (vax-root v) (hide #f))
		     (make-vax
		      vax-state-chasing
		      (parasite-id parasite)
		      (vadd 
		       (parasite-pos parasite) 
		       (receptor-pos receptor))
		      (vax-vel v)
		      (vax-pos v)
		      (vax-root v)
		      #f
		      (vax-type v) (+ (vax-rot v) 1))))))))))
   vs))

(define (vax-destroyed-list vs)
  (foldl
   (lambda (v r)
     (cond 
      ((vax-collide v) 
       (cons (vax-pid v) r)) 
      (else r)))
   '()
   vs))

(define (build-vax-swarm n)
  (build-list n (lambda (_) (build-vax))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define (make-explosion pos type age root) (list pos type age root))

(define (explosion-pos e) (list-ref e 0))
(define (explosion-type e) (list-ref e 1))
(define (explosion-age e) (list-ref e 2))
(define (explosion-root e) (list-ref e 3))

(define (build-explosion pos type)
  (make-explosion pos type 0
		  (with-state
		   (hint-nozwrite)
		   (shader-assign-compiled "explosion")   
		   (colour (type->col type))
		   (load-primitive "models/circlefan.obj"))))

(define (explosion-update e)
  (make-explosion
   (explosion-pos e)
   (explosion-type e)
   (+ (explosion-age e) explosion-speed)
   (explosion-root e)))

(define (explosion-render e)
  (with-primitive
   (explosion-root e)
   (identity)
   (translate (explosion-pos e))
   (let ((sc (* (explosion-age e) (explosion-age e))))
     (scale (vector sc sc sc)))))

(define (explosions-add es pos type)
  (cons (build-explosion pos type) es))

(define (explosions-update es)
  (foldl 
   (lambda (e r)
     (cond 
      ((> (explosion-age e) explosion-max-age)
       (destroy (explosion-root e))
       r)      
      (else
       (cons (explosion-update e) r))))
   '()
   es))

(define (explosions-render es)
  (for-each (lambda (e) (explosion-render e)) es))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 2D schenanigans

(define syringe-button '())

(define (render-canvas)
  (image-button-update vial1-button 0)
  (image-button-update vial2-button 0)
  (image-button-update vial3-button 0)
  (image-button-update vial4-button 0)
  (animated-button-update syringe-button 0)

  (animated-button-render! syringe-button)
  (image-button-render! vial1-button)
  (image-button-render! vial2-button)
  (image-button-render! vial3-button)
  (image-button-render! vial4-button)

  (progress-bar-render! infect-bar)

  (render-popups!)
  
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; preload things

(shader-compile "default" (list "p" "n" "t" "c")
		"shaders/default.vert"
		"shaders/default.frag")
(shader-compile "parasite" (list "p" "n" "t" "c")
		"shaders/parasite.vert"
		"shaders/parasite.frag")
(shader-compile "receptor" (list "p" "n" "t" "c")
		"shaders/receptor.vert"
		"shaders/receptor.frag")
(shader-compile "vax" (list "p" "n" "t" "c")
		"shaders/vax.vert"
		"shaders/vax.frag")
(shader-compile "explosion" (list "p" "n" "t" "c")
		"shaders/explosion.vert"
		"shaders/explosion.frag")


(load-texture "mal1-1.png")
(load-texture "mal1-2.png")
(load-texture "mal1-3.png")
(load-texture "circle-trans.png")
(load-texture "noise.png")
(load-texture "recep-square.png")
(load-texture "recep-circle.png")
(load-texture "recep-tri.png")
(load-texture "recep-diamond.png")
(load-texture "vax.png")

(load-mesh-cache "models/plane.obj")
(load-mesh-cache "models/planesmall.obj")
(load-mesh-cache "models/circlefan.obj")
(load-mesh-cache "models/circlemesh.obj")
(load-mesh-cache "models/circlefansmall.obj")
(load-mesh-cache "models/malaria-low.obj")
(load-mesh-cache "models/malaria.obj")

(load-images!
 (list
  "syringe.png"
  "plunger.png"
  "vial.png"
  "vial-a.png"
  "vial-b.png"
  "vial-c.png"
  "vial-d.png"
  )
 (lambda ()
   0))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define parasites '())
(define vax-swarm '())
(define explosions '())

(define recep-dummy '())
(define vax-dummy '())

(define current-vax "red")
(define current-magic (choose receptor-types))

(define new-game-time (time))
(define vaccinations 0)
(define warning-fired 0)
(define game-start-time (time))
(define reset-game #t)
(define vaccination-run 0)

(define (reset-game!)
  (msg (length parasites))
  (set! reset-game #f)
  (set! vaccinations 0)
  (set! warning-fired 0)
  (set! game-start-time (time))
  (set! current-magic (choose receptor-types))
  (set! vax-swarm (vax-idle vax-swarm))
  (parasites-destroy-all parasites)
  (set! parasites (build-parasites parasites-start-num current-magic)))

(define (render-game)
  (resize-glcanvas webgl-canvas)
  (resize-canvas canvas)
  (explosions-render explosions)

  (when (> (time) new-game-time)
	(when reset-game (reset-game!))	
	(set! parasites (update-parasites parasites))
	(set! parasites (parasites-destroy parasites (vax-destroyed-list vax-swarm)))
	(let ((p (* (/ (length parasites) parasite-max-pop) 100)))
	  (set! infect-bar (progress-bar-update-p infect-bar p))
	  (when (and (> p 70) (not warning-fired))
		(set! warning-fired #t)
		(fire-popup! "bad" (list "WARNING:" 
					 "INFECTION SPREADING!!")))
	  (when (and warning-fired (< p 70)) 
		(set! warning-fired #f))))
  
  (set! vax-swarm (update-vax-swarm vax-swarm parasites))
  (set! explosions (explosions-update explosions))
  
  
  (ctx.clearRect 0 0 screen-width screen-height)	  
  (render-canvas)

  (let ((t (* (time) 1)))
    (with-primitive parasite-dummy (shader-shared-uniform! "time" "f" t))
    (with-primitive recep-dummy (shader-shared-uniform! "time" "f" t))
    (with-primitive vax-dummy (shader-shared-uniform! "time" "f" t)))
  
  )

(define init #f)

(define (build-vial x y type)
  (make-image-button 
   x y 
   (cond 
    ((eq? type "A") "vial-a.png")
    ((eq? type "B") "vial-b.png")
    ((eq? type "C") "vial-c.png")
    (else "vial-d.png"))
   "" 
   (lambda (x) 
     (set! vax-swarm (vax-in-syringe vax-swarm type))
     (when (> (animated-button-time syringe-button) 75)
	   (set! syringe-button
		 (animated-button-modify-time 
		  (animated-button-modify-state 
		   syringe-button "filling") 0))))))


(define (run-init)
  (resize-glcanvas webgl-canvas)
  (canvas-setup)
  (resize-canvas canvas)

  (set! vax-swarm (vax-inject (build-vax-swarm vax-swarm-size)))
  (set! parasites (build-parasites parasites-start-num current-magic))

  (set! recep-dummy (with-state
		     (translate (vector -1000 0 0))
		     (shader-assign-compiled "receptor")   
		     (build-cube)))
  (set! vax-dummy (with-state
		     (translate (vector -1000 0 0))
		     (shader-assign-compiled "vax")   
		     (build-cube)))
  (set! parasite-dummy (with-state
			(translate (vector -1000 0 0))
			(shader-assign-compiled "parasite")   
			(build-cube)))

  (set! vial1-button (build-vial (- (/ screen-width 2) 200) 600 "A"))
  (set! vial2-button (build-vial (- (/ screen-width 2) 100) 600 "B"))
  (set! vial3-button (build-vial (+ (/ screen-width 2) 100) 600 "C"))
  (set! vial4-button (build-vial (+ (/ screen-width 2) 200) 600 "D"))
  (set! infect-bar (make-progress-bar 30 10 (- screen-width 60) 30 "MALARIA INFECTION:" "20pt Dosis" 50))
  
  (set! syringe-button 
	(make-animated-button 
	 (/ screen-width 2) 540 "syringe.png" 
	 "plunger.png" 
	 (lambda (x) 
	   (when (and (eq? (animated-button-state syringe-button) "filling")
		      (> (animated-button-time syringe-button) 75))
		 (set! vaccinations (+ vaccinations 1))
		 (set! syringe-button
		       (animated-button-modify-time 
			(animated-button-modify-state 
			 syringe-button "emptying") 0))				 
		 (set! vax-swarm (vax-inject vax-swarm))))))

  (set-camera-transform (mtranslate (camera-transform) (vector 0 5 0)))
  (clear-colour (list 0.0 0.0 0.0 0.0)))

(define (flot)
  (when  (and
	  (not init)
	  (meshes-loaded?)
	  (textures-loaded?)
	  (shaders-loaded?)
	  (canvas-loaded?)
	  )
	 (set! init #t)
	 (run-init)	 
	 (every-frame render-game)))

(every-frame (flot))
	 
